import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

apply plugin: 'com.bmuschko.docker-remote-api'
apply plugin: 'org.springframework.boot'

dependencies {
    // handy for creating metadata with @ConfigurationProperties
    optional "org.springframework.boot:spring-boot-configuration-processor"

    compile 'org.springframework.cloud:spring-cloud-starter-config'
    compile 'org.opentestsystem.rdw.common:rdw-common-utils'

    // enables email
    compile 'org.springframework.boot:spring-boot-starter-mail'

    // csv
    compile group: 'net.sf.supercsv', name: 'super-csv', version: '2.4.0'

    // auth
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.security.oauth:spring-security-oauth2'
    testCompile 'org.springframework.security:spring-security-test'

    // this is for retrying connection to config server
    compile 'org.springframework.boot:spring-boot-starter-aop'
    compile 'org.springframework.retry:spring-retry'

    compile 'mysql:mysql-connector-java'

    compile 'org.opentestsystem.rdw.common:rdw-common-status'
    compile project(':rdw-ingest-common')

    testCompile 'org.springframework.boot:spring-boot-starter-test'

    // fixes java 8 time marshaling
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.7'
}

// this is for creating metadata
compileJava.dependsOn(processResources)

classes.dependsOn(createBuildInfoFile)

bootRun {
    addResources = true
    if (project.hasProperty('jvmArgs')) {
        jvmArgs project.jvmArgs.split('\\s+')
    }
}

// task to build the docker image
task buildImage(type:DockerBuildImage, dependsOn:[build, createDockerfile]) {
    dockerFile = createDockerfile.destFile
    inputDir = project.file('build/docker/')
    tag = "fwsbac/${project.name}"
    doFirst {
        copy {
            from jar
            into inputDir
        }
    }
}

// to use this, you must use docker login to set up docker hub credentials
task pushImage(type:Exec, dependsOn:[buildImage]) {
    commandLine("docker", "push", "fwsbac/${project.name}")
}
